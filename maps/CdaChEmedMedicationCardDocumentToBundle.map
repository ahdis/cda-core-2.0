map "http://hl7.org/fhir/cda/mapping/CdaChEmedMedicationCardDocumentToBundle" = "CDA MedicationCardDocument to FHIR"

uses "http://hl7.org/fhir/cda/StructureDefinition/ClinicalDocument" alias ClinicalDocument as source
uses "http://hl7.org/fhir/cda/StructureDefinition/AssignedAuthor" alias AssignedAuthor as queried
uses "http://hl7.org/fhir/cda/StructureDefinition/AssignedEntity" alias AssignedEntity as queried
uses "http://hl7.org/fhir/cda/StructureDefinition/CustodianOrganization" alias CustodianOrganization as queried
uses "http://hl7.org/fhir/cda/StructureDefinition/Section" alias Section as queried
uses "http://hl7.org/fhir/cda/StructureDefinition/PatientRole" alias PatientRole as queried
uses "http://hl7.org/fhir/cda/StructureDefinition/SubstanceAdministration" alias SubstanceAdministration as source
uses "http://hl7.org/fhir/cda/StructureDefinition/IVL_TS" alias IVL_TS as source
uses "http://hl7.org/fhir/cda/StructureDefinition/EIVL_TS" alias EIVL_TS as source
uses "http://hl7.org/fhir/cda/StructureDefinition/SXPR_TS" alias SXPR_TS as source

uses "http://hl7.org/fhir/StructureDefinition/Bundle" alias Bundle as target
uses "http://hl7.org/fhir/StructureDefinition/Composition" alias Composition as produced
uses "http://hl7.org/fhir/StructureDefinition/Patient" alias Patient as produced
uses "http://hl7.org/fhir/StructureDefinition/Practitioner" alias Practitioner as produced
uses "http://hl7.org/fhir/StructureDefinition/Organization" alias Organization as produced
uses "http://hl7.org/fhir/StructureDefinition/MedicationStatement" alias Organization as produced
uses "http://hl7.org/fhir/StructureDefinition/Dosage" alias Dosage as produced

imports "http://hl7.org/fhir/cda/mapping/CdaToFhirTypes"
imports "http://hl7.org/fhir/cda/mapping/CdaToBundle"
imports "http://hl7.org/fhir/cda/mapping/CdaChToBundle"

group CdaChEmedMedicationCardDocumentToBundle(source cda : ClinicalDocument, target bundle : Bundle) {
  cda ->  bundle.entry as e first,  e.resource = create('Composition') as composition share composition, 
             bundle.entry as e2, e2.resource = create('Patient') as patient share patient
             then ClinicalDocumentChEmedMedicationCardDocumentToBundle(cda, patient, composition, bundle) "ClinicalDocumentToBody";
}

group ClinicalDocumentChEmedMedicationCardDocumentToBundle(source cda : ClinicalDocument, target patient : Patient, target composition : Composition, target bundle : Bundle) extends ClinicalDocumentChToBundle {
  cda.component as component then {
    component.structuredBody as body then {
      body.component as component then {
        component.section as srcSection where (templateId.where(root='2.16.756.5.30.1.1.10.3.9')) -> composition.section as tgtSection then SectionHistoryOfMedicationUse(srcSection, patient, tgtSection, bundle);
      } "component";
    } "body";
  } "component";
}

group SubstanceAdministrationDosage(source source : SubstanceAdministration, target dosage: Dosage) {
  source.effectiveTime: IVL_TS as effectiveTime -> dosage.timing as timing share sharetiming then {
    effectiveTime -> timing.repeat as repeat then {
      effectiveTime -> repeat.bounds = create('Period') as period then {
        effectiveTime.low as low then {
          low.value as lowDate -> period.start = lowDate "lowDate";
        } "low";
        effectiveTime.high as high then {
          high.value as highDate -> period.end = highDate "highDate";
        } "high";
      } "period";
    } "repeat";
  } "effectiveTime IVL_TS";
  source.effectiveTime: EIVL_TS as effectiveTime -> dosage.timing as timing share sharetiming then {
    effectiveTime -> timing.repeat as repeat then {
      effectiveTime.event as event then {
        event.code as code -> repeat.when = code "code";
      } "event";
    } "repeat";
  } "effectiveTime EIVL_TS";
  source.effectiveTime: SXPR_TS as effectiveTime -> dosage.timing as timing share sharetiming then {
    effectiveTime -> timing.repeat as repeat then {
      effectiveTime.comp as comp then {
        comp.event as event then {
          event.code as code -> repeat.when = code "code";
        } "event";
      } "comp";
    } "repeat";
  } "effectiveTime SXPR_TS";
  source.routeCode as routeCode -> dosage.route as route then CECodeableConcept(routeCode, route) "routeCode";
  source.doseQuantity as doseQuantity -> dosage.doseAndRate as doseAndRate then {
    doseQuantity.center as center -> doseAndRate.dose = create('Quantity') as quantity then {
      center.value as value -> quantity.value = value "value";
    } "quantity";
  } "doseQuantity";
}

group SubstanceAdministrationDosageSplit(source entryRelationship, target dosage: Dosage) {
  entryRelationship.sequenceNumber as sequenceNumber -> dosage.sequence = sequenceNumber "sequenceNumber";
  entryRelationship.substanceAdministration as substanceAdministration then SubstanceAdministrationDosage(substanceAdministration, dosage) "dosage";
}

group SubstanceAdministrationMedication(source source : SubstanceAdministration, target medicationStatement : MedicationStatement, target medication : Medication) {
  source -> medication.id = 'med', medicationStatement.medication = create('Reference') as vt, vt.reference = '#med' "medication";
  source.consumable as consumable then {
    consumable.manufacturedProduct as manufacturedProduct then {
      manufacturedProduct.manufacturedMaterial as manufacturedMaterial then {
        manufacturedMaterial.asContent as asContent then {
          asContent.containerPackagedMedicine as containerPackagedMedicine then {
            containerPackagedMedicine.code as code -> medication.code as fhircode then CECodeableConcept(code, fhircode) "medication.code";
            containerPackagedMedicine.formCode as formCode -> medication.form as form then CECodeableConcept(formCode, form)  "medication.formCode";
            containerPackagedMedicine.capacityQuantity as capacityQuantity where (%containerPackagedMedicine.formCode.code='10219000') -> medication.amount = create('Ratio') as ratio then {
              capacityQuantity.value as value -> ratio.numerator = create('Quantity') as quantity, 
                       quantity.value = value, 
                       quantity.unit='tablets', 
                       quantity.system='http://unitsofmeasure.org',
                       quantity.code='1',
                       ratio.denominator	 = create('Quantity') as denominator
                       , 
                       denominator.value='1',
                       denominator.unit='Package',
                       denominator.system='http://unitsofmeasure.org',
                       denominator.code='1'
                       "capacityQuantity"; 
            } "ratio";
          } "containerPackagedMedicine";
        } "asContent";
        manufacturedMaterial.ingredient as ingredient -> medication.ingredient as ing then {
          ingredient.quantity as quantity -> ing.strength = create('Ratio') as strength then RTOPQPQRatio(quantity, strength) "strength";
          ingredient.ingredient as medingredient then {
              medingredient.code as code -> ing.item = create('CodeableConcept') as ingcode then CECodeableConcept(code, ingcode) "ingredientcode";
          } "ingredient.ingredient";
        } "ingredient";
      } "manufacturedMaterial";
    } "manufacturedProduct";
    source -> medicationStatement.dosage as dosage then SubstanceAdministrationDosage(source, dosage) "dosage";
  } "consumable";
}

group SubstanceAdministrationDosageInstructionUnstructured(source section : Section, source entry : Element, target dosage : Dosage) {
  // see SubstanceAdministrationMedicationStatement
  entry.substanceAdministration as observation -> dosage.text = (%section.text.substring(%section.text.indexOf(%observation.text.reference.value.substring(1))).substring(%section.text.substring(%section.text.indexOf(%observation.text.reference.value.substring(1))).indexOf('>')+1, %section.text.substring(%section.text.indexOf(%observation.text.reference.value.substring(1))).indexOf('<')-%section.text.substring(%section.text.indexOf(%observation.text.reference.value.substring(1))).indexOf('>')-1)) "idRef"; 
  entry.substanceAdministration as observation then {
    observation.text as text then {
      text.reference as reference then {
        reference.value as value -> dosage.extension as ext then NarrativeLink(value, ext) "narrativeLink";
      } "reference";
    } "text";
  } "observation";
}

group ChExternalDocumentId(source source, target ext: Extension) {
  source -> ext.url = 'externalDocumentId' "url";
  source -> ext.value = create('Identifier') as id then II(source, id) "value";
}

group ChExternalDocumentItemId(source source, target ext: Extension) {
  source -> ext.url = 'id' "url";
  source -> ext.value = create('Identifier') as id then II(source, id) "value";
}

group NarrativeLink(source url, target ext: Extension) {
  url -> ext.url = 'http://hl7.org/fhir/StructureDefinition/narrativeLink' "url";
  url -> ext.value = create('url') as value, value.value=url "value";
}

group MTPReferenceEntryContentModule(source entryrelationship, target ext: Extension) {
  entryrelationship -> ext.url = 'http://fhir.ch/ig/ch-emed/StructureDefinition/ch-emed-ext-treatmentplan' "url";
  entryrelationship.substanceAdministration as substanceAdministration then {
    substanceAdministration.id as id -> ext.extension as ext then ChExternalDocumentItemId(id, ext) "ChExternalDocumentItemId";
    substanceAdministration.reference as reference then {
      reference.externalDocument as externalDocument then {
        externalDocument.id as id -> ext.extension as ext then ChExternalDocumentId(id, ext) "ChExternalDocumentId";
      } "externalDocument";
    } "substanceAdministration";
  } "id";
}

group SubstanceAdministrationMedicationStatement(source section : Section, source source : SubstanceAdministration, source patient : Patient, target medicationStatement : MedicationStatement) {
  source.templateId as template then TemplateID(template, medicationStatement) "templateID";
  source.id -> medicationStatement.identifier;
  patient -> medicationStatement.subject = create('Reference') as reference, reference.reference = reference(patient) "patient";
  source -> medicationStatement.status = 'completed' "completed";
  source -> medicationStatement.contained = create('Medication') as medication then SubstanceAdministrationMedication(source, medicationStatement, medication) "medication";
  source.entryRelationship as entry where typeCode = 'RSON' -> medicationStatement.reasonCode as reasonCode then {
    // extraxt text (Bluthochdruck) from id #mtpc.1.reason in section text <td ID="mtpc.1.reason">Bluthochdruck</td>
    //                                                                             a--------------b------------c
    // id = (%observation.text.reference.value.substring(1))
    // a = %section.text.substring(%section.text.indexOf(%id))
    // res = (%a.substring(%a.indexOf('>')+1, %a.indexOf('<')-%a.indexOf('>')-1))
    // a = %section.text.substring(%section.text.indexOf((%observation.text.reference.value.substring(1))))
    entry.observation as observation -> reasonCode.text =  (%section.text.substring(%section.text.indexOf(%observation.text.reference.value.substring(1))).substring(%section.text.substring(%section.text.indexOf(%observation.text.reference.value.substring(1))).indexOf('>')+1, %section.text.substring(%section.text.indexOf(%observation.text.reference.value.substring(1))).indexOf('<')-%section.text.substring(%section.text.indexOf(%observation.text.reference.value.substring(1))).indexOf('>')-1)) "idRef"; 
    entry.observation as observation then {
      observation.text as text then {
        text.reference as reference then {
          reference.value as value -> reasonCode.extension as ext then NarrativeLink(value, ext) "narrativeLink";
        } "reference";
      } "text";
    } "observation";
  } "entryRelationShip 2.16.756.5.30.1.1.10.4.41";
  source.entryRelationship as entry where (typeCode='COMP' and act.templateId.root='2.16.756.5.30.1.1.10.4.2') -> medicationStatement.note as note then {
    entry.act as act -> note.text =  (%section.text.substring(%section.text.indexOf(%act.text.reference.value.substring(1))).substring(%section.text.substring(%section.text.indexOf(%act.text.reference.value.substring(1))).indexOf('>')+1, %section.text.substring(%section.text.indexOf(%act.text.reference.value.substring(1))).indexOf('<')-%section.text.substring(%section.text.indexOf(%act.text.reference.value.substring(1))).indexOf('>')-1)) "idRef"; 
    entry.act as act then {
      act.text as text then {
        text.reference as reference then {
          reference.value as value -> dosage.extension as ext then NarrativeLink(value, ext) "narrativeLink";
        } "reference";
      } "text";
    } "actnarrative";
  } "entryRelationShip 2.16.756.5.30.1.1.10.4.2";  
  source.entryRelationship as entry where (typeCode='COMP' and substanceAdministration.templateId.root='2.16.756.5.30.1.1.10.4.37') -> medicationStatement.dosage as dosage 
    then SubstanceAdministrationDosageInstructionUnstructured(section, entry, dosage) "entryRelationShip 2.16.756.5.30.1.1.10.4.37";  
  source.entryRelationship as entry where (typeCode='COMP' and sequenceNumber.value>=0) -> medicationStatement.dosage as dosage 
    then SubstanceAdministrationDosageSplit(entry, dosage) "entryRelationShip 2.16.756.5.30.1.1.10.4.36";  
  source.entryRelationship as entry where (typeCode='REFR' and substanceAdministration.templateId.where(root='1.3.6.1.4.1.19376.1.9.1.3.10').exists()) -> medicationStatement.extension as ext then MTPReferenceEntryContentModule(entry, ext) "MTPReferenceEntry";   
}

group SectionHistoryOfMedicationUse(source source : Section, source patient : Patient, target target, target bundle: Bundle) extends ClinicalDocumentSection {
  source.entry as cdaEntry ->  bundle.entry as e,  e.resource = create('MedicationStatement') as medicationstatement,  target.entry = create('Reference') as reference, reference.reference = reference(medicationstatement) then {
    cdaEntry.substanceAdministration as substanceAdministration then SubstanceAdministrationMedicationStatement(source, substanceAdministration, patient, medicationstatement);
  };
}